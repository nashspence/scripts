#!/bin/zsh
set -euo pipefail

source "$HOME/.config/podman-scripts/shortcuts_path.zsh"

INPUT="${1:-}"
if [[ -z "$INPUT" ]]; then
  print -u2 -- "no input file path provided"
  exit 2
fi

NOW=$(date +%Y%m%dT%H%M%S)
OUT="$HOME/Pipelines/Archive Videos/$NOW"
mkdir -p "$OUT"

STAGE_OUT="$OUT/stage"
VCRUNCH_OUT="$OUT/vcrunch"
MKISO_OUT="$OUT/mkiso"
QCUT_OUT="$OUT/qcut"
mkdir -p "$STAGE_OUT" "$VCRUNCH_OUT" "$MKISO_OUT" "$QCUT_OUT"

export DR_NO_TTY=1
caffeinate -dims -w $$ &

source "$HOME/bin/lib/proc_supervisor.zsh"
proc_init
proc_traps

proc_run   "stage"   -v "${INPUT}:/in"      -v "${STAGE_OUT}:/out"          || exit $?
proc_run   "vcrunch" -v "${STAGE_OUT}:/in"  -v "${VCRUNCH_OUT}:/out"        || exit $?

typeset mkiso_pid qcut_pid
proc_run_bg mkiso_pid   "mkiso"    -v "${VCRUNCH_OUT}:/in" -v "${MKISO_OUT}:/out"    -- --out-file "$NOW.iso"
proc_run_bg qcut_pid    "qcut"     -v "${STAGE_OUT}:/in"   -v "${QCUT_OUT}:/out"

proc_wait "$mkiso_pid"  || { rc=$?; proc_kill_and_wait "$qcut_pid"; exit $rc; }
proc_run  burniso -v "${MKISO_OUT}/${NOW}.iso" || { rc=$?; proc_kill_and_wait "$qcut_pid"; exit $rc; }

proc_wait "$qcut_pid" || exit $?

print -r -- "[$(date -u +%FT%TZ)] done"
exit 0
